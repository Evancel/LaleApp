plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco' // optional but recommended
}

description = 'user-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'commons-validator:commons-validator:1.7'

    // DB drivers
    runtimeOnly 'org.postgresql:postgresql'
    testRuntimeOnly 'com.h2database:h2'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    // If you generate mappers inside test sources too:
    // testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    //JWT-token
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5' // for JSON serialization

    //Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    // Spring Boot test stack (includes JUnit5, AssertJ, Mockito, JSONAssert, etc.)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "net.jqwik:jqwik:1.8.4"

    // Integration tests with PostgreSQL
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('smokeTest', Test) {
    description = 'Runs smoke tests (fast, high-level checks)'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'smoke'
    }

    // reuse main test classpath
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    shouldRunAfter test // optional, just for ordering
}

jacoco { toolVersion = '0.8.11' } // optional